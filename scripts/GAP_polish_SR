"""
This pipeline polishes the genomes
"""

configfile: "config.yaml"
maxthreads = 90

rule all:
    input:
        expand("assemblies/pilon1/output/{nom}_pilon1_output.fasta", nom=config["assemblies"]),

rule pilon1_index:
    """
    pilon1 is the last step
    """
    input:
        assem = lambda wildcards: config["assemblies"][wildcards.nom]
    output:
        assem = "assemblies/pilon1/input/{nom}_pilon1_input.fasta",
        amb   = "assemblies/pilon1/input/{nom}_pilon1_input.fasta.amb",
        ann   = "assemblies/pilon1/input/{nom}_pilon1_input.fasta.ann",
        bwt   = "assemblies/pilon1/input/{nom}_pilon1_input.fasta.bwt",
        pac   = "assemblies/pilon1/input/{nom}_pilon1_input.fasta.pac",
    params:
        pilonround = 1
    threads:
        1
    shell:
        """
        ln -s {input.assem} {output.assem}
        bwa index -a is {output.assem}
        """

rule pilon1_map:
    input:
        assem = "assemblies/pilon1/input/{nom}_pilon1_input.fasta",
        amb   = "assemblies/pilon1/input/{nom}_pilon1_input.fasta.amb",
        ann   = "assemblies/pilon1/input/{nom}_pilon1_input.fasta.ann",
        bwt   = "assemblies/pilon1/input/{nom}_pilon1_input.fasta.bwt",
        pac   = "assemblies/pilon1/input/{nom}_pilon1_input.fasta.pac",
        shotf = config["shotf"],
        shotr = config["shotr"],
    output:
        bam = "bams/pilon1/shot_to_{nom}_pilon1_input.sorted.bam",
    threads:
        maxthreads
    params:
        pilonround = 1
    shell:
        """
        bwa mem -t {threads} {input.assem} \
                 {input.shotf} {input.shotr} | \
                 samtools view -hb -@ {threads} - | \
                 samtools sort -@ {threads} - > {output.bam}
        """

rule pilon1_indexbam:
    input:
        bam = "bams/pilon1/shot_to_{nom}_pilon1_input.sorted.bam",
    output:
        bai = "bams/pilon1/shot_to_{nom}_pilon1_input.sorted.bam.bai",
    threads:
        1
    shell:
        """
        samtools index {input.bam}
        """

rule pilon1_piloncorrect:
    input:
        assem = "assemblies/pilon1/input/{nom}_pilon1_input.fasta",
        bam = "bams/pilon1/shot_to_{nom}_pilon1_input.sorted.bam",
        bai = "bams/pilon1/shot_to_{nom}_pilon1_input.sorted.bam.bai",
        pilon = config["pilonpath"]
    output:
        assem = "assemblies/pilon1/output/{nom}_pilon1_output.fasta"
    threads:
        maxthreads
    params:
        pilonround = 1
    shell:
        """
        java -Xmx100G -jar {input.pilon} --genome {input.assem} \
                 --frags {input.bam} \
                 --outdir assemblies/pilon{params.pilonround}/output/ \
                 --output {wildcards.nom}_pilon{params.pilonround}_output \
                 --threads {threads}
        sed -i 's/_pilon//g' assemblies/pilon{params.pilonround}/output/{wildcards.nom}_pilon{params.pilonround}_output.fasta
        """
